#version 460
#extension GL_EXT_ray_tracing : require

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;

layout(push_constant) uniform PC {
    vec3 lightDir;
    float time;
    vec3 camPos;
    float camFov;
    vec3 camRight;
    float _pad1;
    vec3 camUp;
    float _pad2;
    vec3 camFwd;
    float _pad3;
};

struct RayPayload {
    vec3 color;
    int  depth;
};

layout(location = 0) rayPayloadEXT RayPayload payload;

void main() {
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 uv = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = uv * 2.0 - 1.0;
    d.y = -d.y;  // Vulkan Y-axis points down, flip for camera

    float aspect = float(gl_LaunchSizeEXT.x) / float(gl_LaunchSizeEXT.y);

    vec3 origin    = camPos;
    vec3 direction = normalize(camFwd + camRight * d.x * aspect * camFov
                                      + camUp    * d.y * camFov);

    payload.color = vec3(0.0);
    payload.depth = 0;

    traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xFF,
                0, 0, 0,
                origin, 0.001, direction, 100.0,
                0);

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(payload.color, 1.0));
}
